apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'trikita.module'

android {
	flavorDimensions "api"
	productFlavors {
		sdk15 {
			minSdkVersion 15
		}
		sdk19 {
			minSdkVersion 19
		}
		sdk21 {
			minSdkVersion 21
		}
	}
}

anvilgen {
	type = 'sdk'
	manualSetterName = 'CustomDslSetter'
	quirks = [
		// This requires lots of type checks, skipping this factory method for now
		'android.widget.AutoCompleteTextView' : [
			'setAdapter': false
		],

		// Block setAdapter(ListAdapter) and setAdapter(SpinnerAdapter), leave only parent setAdapter(Adapter) instead
		'android.widget.AbsListView' : [
			'setAdapter': false
		],
		'android.widget.ListView' : [
			'setAdapter': false
		],
		'android.widget.GridView' : [
			'setAdapter': false
		],
		'android.widget.AbsSpinner' : [
			'setAdapter': false
		],
		'android.widget.Spinner' : [
			'setAdapter': false
		],
		'android.widget.ExpandableListView' : [
			'setAdapter:android.widget.ListAdapter': false
		],

		// Exception must be checked in this attribute setter
		'android.widget.TextView': [
			'setText:kotlin.CharSequence': false,
			'setTextSize': false, // broken: uses 'sp' unit by default
			'setInputExtras': false
		],

		'android.widget.Switch': [
			'__viewAlias': 'SwitchView'
		],

		// This will remove the whole builder, so no factory method will be generated
		'android.renderscript.RSSurfaceView': [
			'__viewAlias': false,
		],

		// This will remove the whole builder, so no factory method will be generated
		'android.renderscript.RSTextureView': [
			'__viewAlias': false,
		],

		// TODO This is a static method
		'android.webkit.WebView': [
			'setWebContentsDebuggingEnabled': false,
		],
	]
}

dependencies {
	implementation "androidx.annotation:annotation:$androidx_version"

	testImplementation 'junit:junit:4.12'
	testImplementation "org.mockito:mockito-core:$mockito_version"
}
